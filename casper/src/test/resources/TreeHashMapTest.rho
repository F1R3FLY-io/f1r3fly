new
  rl(`rho:registry:lookup`), RhoSpecCh,
  stdlog(`rho:io:stdlog`),
  testGetBeforeSet,
  testGetAfterSet,
  testGetAfterUpdateNil,
  testGetAfterUpdate,
  testFastUnsafeGetAfterSet,
  testContainsBeforeSet,
  testContainsAfterSet,
  testGetAfterSetToNil,
  testContainsAfterSetToNil,
  testContention
in {
  rl!(`rho:id:zphjgsfy13h1k85isc8rtwtgt3t9zzt5pjd5ihykfmyapfc4wt3x5h`, *RhoSpecCh) |
  for(@(_, RhoSpec) <- RhoSpecCh) {
    @RhoSpec!("testSuite",
      [
        ("Get before set returns Nil", *testGetBeforeSet),
        ("Get after set returns the new value", *testGetAfterSet),
        ("Get after updating a nonexistent key should return Nil", *testGetAfterUpdateNil),
        ("Get after updating an existing key should return the right value", *testGetAfterUpdate),
        ("Fast unsafe get after set returns the new value", *testFastUnsafeGetAfterSet),
        ("Contains before set returns false", *testContainsBeforeSet),
        ("Contains after set returns true", *testContainsAfterSet),
        ("Get after set to Nil returns Nil", *testGetAfterSetToNil),
        ("Contains after set to Nil returns true", *testContainsAfterSetToNil),
        ("Works under contention", *testContention)
      ])
  } |

  new TreeHashMapCh in {
    rl!(`rho:lang:treeHashMap`, *TreeHashMapCh) |
    for(TreeHashMap <- TreeHashMapCh) {
      contract testGetBeforeSet(rhoSpec, _, ackCh) = {
        new ret, ch in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("get", thm, "no such key", *ch) |
            rhoSpec!("assertMany",
              [
                ((Nil, "== <-", *ch), "Getting with an unused key returns Nil")
              ], *ackCh)
          }
        }
      } |

      contract testGetAfterUpdateNil(rhoSpec, _, ackCh) = {
        new thmCh, ack, ch, update in {
          TreeHashMap!("init", 3, *thmCh) |
          contract update(@val, ret) = {
            ret!(val + 1)
          } |
          for (@thm <- thmCh) {
            TreeHashMap!("update", thm, "no such key", *update, *ack) |
            for (_ <- ack) {
              TreeHashMap!("get", thm, "no such key", *ch) |
              rhoSpec!("assertMany",
                [
                  ((Nil, "== <-", *ch), "Getting after updating with an unused key returns Nil")
                ], *ackCh)
            }
          }
        }
      } |

      contract testGetAfterUpdate(rhoSpec, _, ackCh) = {
        new thmCh, ack1, ack2, ch, update in {
          TreeHashMap!("init", 3, *thmCh) |
          contract update(@val, ret) = {
            ret!(val + 1)
          } |
          for (@thm <- thmCh) {
            TreeHashMap!("set", thm, "key", 0, *ack1) |
            for (_ <- ack1) {
              TreeHashMap!("update", thm, "key", *update, *ack2) |
              for (_ <- ack2) {
                TreeHashMap!("get", thm, "key", *ch) |
                rhoSpec!("assertMany",
                  [
                    ((1, "== <-", *ch), "Getting after updating after setting returns the right value")
                  ], *ackCh)
              }
            }
          }
        }
      } |

      contract testGetAfterSet(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("get", thm, "some key", *ch1) |
              TreeHashMap!("get", thm, "some other key", *ch2) |
              rhoSpec!("assertMany",
                [
                  (("some val", "== <-", *ch1), "Getting with the proper key returns the correct value"),
                  ((Nil, "== <-", *ch2), "Getting with a different key returns Nil")
                ], *ackCh)
            }
          }
        }
      } |

      contract testFastUnsafeGetAfterSet(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("fastUnsafeGet", thm, "some key", *ch2) |
              rhoSpec!("assertMany",
                [
                  (("some val", "== <-", *ch2), "Getting with the proper key returns the correct value"),
                ], *ackCh)
            }
          }
        }
      } |

      contract testContainsBeforeSet(rhoSpec, _, ackCh) = {
        new ret, ch in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("contains", thm, "no such key", *ch) |
            rhoSpec!("assertMany",
              [
                ((false, "== <-", *ch), "Getting with an unused key returns false")
              ], *ackCh)
          }
        }
      } |

      contract testContainsAfterSet(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2, ch3 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", "some val", *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("contains", thm, "some key", *ch2) |
              TreeHashMap!("contains", thm, "some other key", *ch3) |
              rhoSpec!("assertMany",
                [
                  ((true, "== <-", *ch2), "Getting with the proper key returns true"),
                  ((false, "== <-", *ch3), "Getting with a different key returns false")
                ], *ackCh)
            }
          }
        }
      } |

      contract testGetAfterSetToNil(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", Nil, *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("get", thm, "some key", *ch2) |
              rhoSpec!("assertMany",
                [
                  ((Nil, "== <-", *ch2), "Getting with the proper key returns the correct value"),
                ], *ackCh)
            }
          }
        }
      } |

      contract testContainsAfterSetToNil(rhoSpec, _, ackCh) = {
        new ret, ch1, ch2, ch3 in {
          TreeHashMap!("init", 3, *ret) |
          for (@thm <- ret) {
            TreeHashMap!("set", thm, "some key", Nil, *ch1) |
            for (_ <- ch1) {
              TreeHashMap!("contains", thm, "some key", *ch2) |
              TreeHashMap!("contains", thm, "some other key", *ch3) |
              rhoSpec!("assertMany",
                [
                  ((true, "== <-", *ch2), "Getting with the proper key returns true"),
                  ((false, "== <-", *ch3), "Getting with a different key returns false")
                ], *ackCh)
            }
          }
        }
      } |

      contract testContention(rhoSpec, _, ackCh) = {
        new tryIt in {
          contract tryIt(@n, @flag) = {
            new out(`rho:io:stdlog`), mapCh in {
              TreeHashMap!("init", 3, *mapCh) |
              for (@map <- mapCh) {
                new ret1, ret2, ret3 in {
                  TreeHashMap!("set", map, "category", "object", *ret1) |
                  TreeHashMap!("set", map, "category", "morphism", *ret2) |
                  for (_ <- ret1 & _ <- ret2) {
                    TreeHashMap!("get", map, "category", *ret3) |
                    for (@val3 <- ret3) {
                      out!("info", [n, val3]) |
                      if (n == 9) {
                        rhoSpec!("assertMany",
                        [
                          ((flag, "==", true), "Placeholder")
                        ], *ackCh)
                      } else {
                        tryIt!(n+1, flag and (val3 != Nil))
                      }
                    }
                  }
                }
              }
            }
          } |
          tryIt!(0, true)
        }
      }
    }
  }
}
